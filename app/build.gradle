apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "com.bkromhout.minerva"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            ext.enableCrashlytics = false
        }
        // Workaround for https://code.google.com/p/android/issues/detail?id=123771 if we need coverage.
        instrumented.initWith(buildTypes.debug)
        instrumented {
            testCoverageEnabled true
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        //incremental = false
    }
}

crashlytics {
    enableNdk true
}

apt {
    arguments {
        eventBusIndex "com.bkromhout.minerva.EventBusIndex"
    }
}

dexcount {
    includeClasses = true
    includeTotalMethodCount = true
    orderByMethodCount = true
}

//noinspection GroovyAssignabilityCheck
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:percent:24.0.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    //compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha1'

    compile 'com.github.bkromhout:java-diff-utils:2.1.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    compile 'com.jakewharton:butterknife:8.1.0'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    compile 'com.jakewharton.timber:timber:4.1.2'

    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.artemzin.rxjava:proguard-rules:1.1.6.0' // Proguard rules for RxJava.
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.chrisbanes:PhotoView:1.2.6'
    compile 'com.binaryfork:spanny:1.0.4'
    compile 'com.karumi:dexter:2.2.2' // TODO Get rid of in favor of doing it ourselves.
    compile 'com.afollestad.material-dialogs:core:0.8.6.0'
    compile 'com.afollestad.material-dialogs:commons:0.8.6.0'
    compile('com.mikepenz:aboutlibraries:5.6.8@aar') {
        transitive = true
    }

    compile('com.bkromhout:realm-recyclerview-lite:2.0.5@aar') {
        transitive = true
    }
    apt 'com.bkromhout.ruqus:ruqus-compiler:1.2.2'
    compile('com.bkromhout.ruqus:ruqus-core:1.2.2@aar') {
        transitive = true
    }

    compile('com.crashlytics.sdk.android:crashlytics:2.5.7@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.3@aar') {
        transitive = true
    }

    compile(project(':epublib')) {
        exclude group: 'xmlpull'
        exclude group: 'net.sf.kxml'
    }

    /* Debug */
    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.uphyca:stetho_realm:0.8.0'

    /* Local Tests */
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'

    /* Instrumented Tests */
    androidTestCompile 'com.android.support:support-annotations:24.0.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    // Needed to resolve version conflict for test APK.
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.0'
}
