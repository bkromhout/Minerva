apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.bkromhout.minerva"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            //shrinkResources true TODO After initial proguard config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        //incremental = false
    }
}

apt {
    arguments {
        eventBusIndex "com.bkromhout.minerva.EventBusIndex"
    }
}

dexcount {
    includeClasses = true
    includeTotalMethodCount = true
    orderByMethodCount = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:percent:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'

    compile 'com.google.guava:guava:19.0'
    compile 'com.github.bkromhout:java-diff-utils:2.0.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile 'com.jakewharton.timber:timber:4.1.2'

    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.commit451:PhotoView:1.2.5'
    compile 'com.binaryfork:spanny:1.0.4'
    compile 'com.karumi:dexter:2.2.2' //AL
    compile('com.github.afollestad.material-dialogs:core:0.8.5.8@aar') {
        transitive = true
    }
    compile('com.github.afollestad.material-dialogs:commons:0.8.5.8@aar') {
        transitive = true
    }
    compile('com.mikepenz:aboutlibraries:5.6.5@aar') {
        transitive = true
    }

    compile ('com.bkromhout:realm-recyclerview-lite:2.0.3@aar') {
        transitive = true
    }
    apt 'com.bkromhout.ruqus:ruqus-compiler:1.1.0'
    compile ('com.bkromhout.ruqus:ruqus-core:1.1.0@aar') {
        transitive = true
    }

    compile('nl.siegmann.epublib:epublib-core:3.1') { //AL
        exclude group: 'xmlpull'
        exclude group: 'net.sf.kxml'
    }

    /* Debug */
    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.uphyca:stetho_realm:0.8.0'

    /* Local Tests */
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'

    /* Instrumented Tests */
    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    // Needed to resolve version conflict for test APK.
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.0'
}
